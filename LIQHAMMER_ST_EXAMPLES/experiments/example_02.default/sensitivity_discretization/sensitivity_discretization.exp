 /*-----------------------------------------------------------------------------------------
 LIBRARY: LIQHAMMER_ST_EXAMPLES
 COMPONENT: example_02
 PARTITION: default
 EXPERIMENT: sensitivity_discretization
 TEMPLATE: EMPTY EXPERIMENT 
 CREATION DATE: 16/06/2019
-----------------------------------------------------------------------------------------*/

EXPERIMENT sensitivity_discretization ON example_02.default
   DECLS
		--Two experiment parameters: 
		--		close_time to define the total closure time
		--		n_exp	to define the shape of the closure law (linear reduction with n_exp = 1)
		REAL close_time = 2.1
      REAL n_exp = 0.75
   OBJECTS
   INIT
      -- initial values for algebraics
   BOUNDS
      -- Set equations for boundaries: boundVar = f(TIME;...)
      valve.pos = 1- (min(TIME/close_time,1))**n_exp
   BODY
		------RECOMMENDED OPTIONS FOR LIQHAMMER EXPERIMENTS-------------------------
		-- For Liqhammer cases it is recommended to deactivate the REPORT_TABLE
      --REPORT_TABLE("reportAll.rpt", "*")
      -- Set the tolerances (relative and absolute);
      REL_ERROR = 1e-006
      ABS_ERROR = 1e-006
		--Recommended integration method for LiqHammer models is a explicit one: 
		--		Runge Kutta or Euler
		--		It must be noted that Euler is only supported since Ecosim version 5.4
		IMETHOD = RK4
		--LiqHammer typically uses a very small time steps and a explicit solver, 
		--so it is advantegeous to deactivate the exact finding of events
		FIND_EVENTS = FALSE
		--
		REPORT_MODE = IS_CINT
		-----------------------------------------------------------------------------
		FOR(i IN 1, 8)
			RESET_VARIABLES()			 
			solver.nrsp_min = 2**i
			solver.nrsp_max = 2**i
			NEW_BRANCH("number of reaches = $solver.nrsp_min")
			EXEC_INIT()
      	TIME = 0
      	TSTOP = 5.
      	CINT = 0.01
			INTEG()
		END FOR
END EXPERIMENT

